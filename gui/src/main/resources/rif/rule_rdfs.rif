Document(
	Prefix(rdf     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>)
	Prefix(dc      <http://purl.org/dc/elements/1.1/>)
	Prefix(dcterms <http://purl.org/dc/terms/>)
	Prefix(bench   <http://localhost/vocabulary/bench/>)
	Prefix(xsd     <http://www.w3.org/2001/XMLSchema#>)
	Prefix(rdfs    <http://www.w3.org/2000/01/rdf-schema#>)
	Prefix(pred <http://www.w3.org/2007/rif-builtin-predicate#>)

	(* http://www.w3.org/TR/rdf-mt/#RDFSRules *)
	Group (
		(* TODO rdfs1 *)
		
		(* rdfs2 *)
		Forall ?u ?x ?a ?y(
		  ?u # ?x :- And(?a[rdfs:domain -> ?x] ?u[?a -> ?y])
		)
		
		(* rdfs3 *)
		Forall ?a ?x ?u ?v (
		   ?v # ?x :- And(?a[rdfs:range -> ?x] ?u[?a -> ?v])
		)
		
		(* rdfs4 *)
		Forall ?u ?a ?x (
		   ?u # rdfs:Resource :- ?u[?a -> ?x]
		)
		
		Forall ?u ?a ?x (
		   ?x # rdfs:Resource :- ?u[?a -> ?x]
		)
		
		(* rdfs5 *)
		Forall ?u ?v ?x (
		   ?u[rdfs:subPropertyOf -> ?x] :- And(?u[rdfs:subPropertyOf -> ?v] ?v[rdfs:subPropertyOf -> ?x])
		)
		
		(* rdfs6 *)
		Forall ?u (
		   ?u[rdfs:subPropertyOf -> ?u] :- ?u # rdf:Property  
		)
		
		(* rdfs7 *)
		Forall ?a ?b ?u ?y (
		   ?u[?b -> ?y] :- And(?a[rdfs:subPropertyOf -> ?b] ?u[?a -> ?y])
		)
		
		(* rdfs8 *)
		Forall ?u (
		   ?u ## rdfs:Resource :- ?u # rdfs:Class
		)
		
		(* rdfs9 *)
		Forall ?u ?x ?v (
		   ?v # ?x :- And(?u ## ?x ?v # ?u)
		)
		
		(* rdfs10 *)
		Forall ?u (
		   ?u ## ?u :- ?u # rdfs:Class
		)
		
		(* rdfs11 *)
		Forall ?u ?x ?v (
		   ?u ## ?x :- And(?u ## ?v ?v ## ?x)
		)
		
		(* rdfs12 *)
		Forall ?u (
		   ?u[rdfs:subPropertyOf -> rdfs:member] :- ?u # rdfs:ContainerMembershipProperty
		)
		
		(* rdfs13 *)
		Forall ?u (
		   ?u ## rdfs:Literal :- ?u # rdfs:Datatype
		)
		
		
		
		
	(* ext1 *)
	Forall ?p ?c1 ?c2 (
   		?p[rdfs:domain->?c2] :- And(
       		?p[rdfs:domain->?c1]
      		?c1[rdfs:subClassOf->?c2]     
     ))

	(* ext3 *)
	Forall ?c ?p2 ?p1 (
   		?p1[rdfs:domain->?c] :- And(
       		?p2[rdfs:domain->?c]
       		?p1[rdfs:subPropertyOf->?p2]         
        ))




	(* ext2 *)
	Forall ?p ?c1 ?c2 (
 	  ?p[rdfs:range->?c2] :- And(
  	     ?p[rdfs:range->?c1]
  	     ?c1[rdfs:subClassOf->?c2]     
        ))


	(* ext4 *)
	Forall ?c ?p2 ?p1 (
   ?p1[rdfs:range->?c] :- And(
       ?p2[rdfs:range->?c]
       ?p1[rdfs:subPropertyOf->?p2]      
	))


	(* ext5 *)
	Forall ?v ?w  (
   rdfs:Resource[rdfs:subClassOf->?v] :- And(
	rdf:type[rdfs:subPropertyOf->?w]
       ?w[rdfs:domain->?v]
       Or(
        External(pred:is-uri(?v))
        External(pred:is-blanknode(?v))
        ) 
  ))



	(* ext6 *)
	Forall ?v ?w  (
   		rdfs:Class[rdfs:subClassOf->?v] :- And(
			rdfs:subClassOf[rdfs:subPropertyOf->?w]
     	    ?w[rdfs:domain->?v]
       		Or(
        		External(pred:is-uri(?v))
        		External(pred:is-blanknode(?v))
        	) 
 ))

	(* ext7 *)
Forall ?v ?w  (
   rdf:Property[rdfs:subClassOf->?v] :- And(
	rdfs:subPropertyOf[rdfs:subPropertyOf->?w]
       ?w[rdfs:domain->?v]
       Or(
        External(pred:is-uri(?v))
        External(pred:is-blanknode(?v))
        ) 
 ))

	(* ext8 *)
Forall ?v ?w  (
   rdfs:Class[rdfs:subClassOf->?v] :- And(
	rdfs:subClassOf[rdfs:subPropertyOf->?w]
       ?w[rdfs:range->?v]
       Or(
        External(pred:is-uri(?v))
        External(pred:is-blanknode(?v))
        ) 
))



	(* ext9 *)
	Forall ?v ?w  (
  		 rdf:Property[rdfs:subClassOf->?v] :- And(
		 rdfs:subPropertyOf[rdfs:subPropertyOf->?w]
      	 ?w[rdfs:range->?v]
       Or(
        External(pred:is-uri(?v))
        External(pred:is-blanknode(?v))
        ) 
         ))
			
		(* rdf1 *)
	Forall ?u ?a ?x (
	?a[rdf:type->rdf:Property]:- And(
		?u[?a->?x]  
		)
)
		
 	)
)